//Creation Co,
//Day      2008.07.09
//Version  2.0
//WEB      http://mosquito.homeftp.net
//Mail     http://mosquito.homeftp.net/cgi/mailplus/index.html 

/*バージョン履歴
ver0.1=若干バグがあるがなんとなく実行可能
ver0.2=オプションが足りないが内部エンジンがようやく完成
ver0.3=文字消しツール追加
ver1.0=完成、プロジェクトにリリース開始。
ver1.1=他のグループ内に同じ名前があると起こるバグを修正。
ver1.2=[reSort]のを強引に行ってたのを直した(グループ内reSort可能に)。
       グループ内のりネームでまだ細かいバグがあったので修正。
ver1.3=Jointなど階層の深い場所で同じ名前でRenameできないバグを修正
ver1.4=アルファベットの後ろ又は前に文字を付けるようにした(Insertからsandwich)。
       インターフェイスを多少調整
ver1.5=名前の前に文字を追加するPrefix、後ろに追加するSuffixを追加。
ver2.0=検索＆置き換えを追加。連番追加。思いつく機能一通りそろったのでVer2とする。
       インターフェイスを日本語と英語で変更出来るようにした。
       リネームに失敗する場合は、エラーで止まらないように"_新しい名前"にするようにした。
*/

/*バグ
・折り返し時にZ桁を飛ばす時がある(順番には影響なし)
・7桁以上はエラー(`pow 26 7`をintに入れるとマイナスになる？)
*/

//Create Window------------------------------------------------------------------------------------------------
global proc MultiRenamer(){//window -q -wh MultiRenamerWindow;
 if((`window -ex MultiRenamerWindow`) == 1)deleteUI MultiRenamerWindow;
 if((`windowPref -ex MultiRenamerWindow`) == 1)windowPref -e -wh 266 162 MultiRenamerWindow;
 window -wh 266 162 -tlb true -s true -t "MultiRenamer Ver2.0" -maximizeButton 0 -resizeToFitChildren 0 -menuBar 1 -menuBarVisible 1 -retain MultiRenamerWindow;
  menu -label "Edit" -tearOff 0 MR_Edit;
  menuItem -label "ResetSettings" -c "deleteUI MultiRenamerWindow; MultiRenamer" MR_Reset;
  menu -label "Language" -tearOff 0 MR_Language;
  menuItem  -label "English"  -c "MultiRenamer_loadEnglish" MR_English;
  menuItem  -label "Japanese"  -c "MultiRenamer_loadJapanese" MR_Japanese;
  menu -label "Help" -tearOff 0 MR_Help;
  menuItem  -label "Readme"  -c "MultiRenamer_ReadmeCommnd" MR_Readme;
  menuItem  -label "HomePeage"  -c "UnitBus" -ann "http://mosquito.homeftp.net/" MR_HomePeage;

  columnLayout -adj 1;
   separator -style "in" -w 266;
   
   frameLayout -la "center" -cll 1 -cl 1 -bv 0 -l "Rename and Alphabet"
    -cc "int $wPh=`window -q -h MultiRenamerWindow`;window -e -h ($wPh-126) MultiRenamerWindow;"
    -ec "int $wPh=`window -q -h MultiRenamerWindow`;window -e -h ($wPh+126) MultiRenamerWindow;"
	MR_RenameAndAlphabet;
    columnLayout;
     checkBox -l "KeepName" -al "left" -v 1 -w 256
      -onc "textFieldGrp -e -en 0 MR_alphaRename;" 
      -ofc "textFieldGrp -e -en 1 MR_alphaRename;" 
      MR_keepCheck;
     textFieldGrp -en 0 -l "Name" -tx "" -cw2 56 200 MR_alphaRename;
     rowLayout -numberOfColumns 3 -columnWidth 1 62 -columnWidth 2 62 -columnWidth 3 130;
      checkBox -l "Front" -v 0 
       -onc "checkBox -e -v 0 MR_backCheck;" 
       -ofc "checkBox -e -v 1 MR_backCheck;" 
       MR_frontCheck;
      checkBox -l "Back" -v 1 
       -onc "checkBox -e -v 0 MR_frontCheck;" 
       -ofc "checkBox -e -v 1 MR_frontCheck;" 
       MR_backCheck;
      textFieldGrp -l "Sandwich" -tx "" -cw2 72 58 MR_InsertionField;
     setParent "..";
     rowLayout -numberOfColumns 2 -columnWidth 1 124 -columnWidth 2 130;
      checkBox -l "EditStart" -v 0 -w 120
       -onc "intFieldGrp -e -en 1 MR_alphaStart;" 
       -ofc "intFieldGrp -e -en 0 MR_alphaStart;" 
       MR_startCheck;
      intFieldGrp -l "StartNumber" -v1 0 -en 0 -cw2 72 58 MR_alphaStart;
     setParent "..";
     rowLayout -numberOfColumns 2 -columnWidth 1 124 -columnWidth 2 130;
      checkBox -l "AutoPadding" -v 1 -w 120
       -onc "intFieldGrp -e -en 0 MR_alphaPadding;" 
       -ofc "intFieldGrp -e -en 1 MR_alphaPadding;" 
       MR_autoCheck;
      intFieldGrp -l "Padding" -v1 0 -en 0 -cw2 72 58 MR_alphaPadding;
     setParent "..";
     rowColumnLayout -numberOfColumns 2 -columnWidth 1 190 -columnWidth 2 66;
      button -l "Rename And Alphabet" -al "center" -c "MR_AlphabetRename" MR_AlphabetRenameButton;
      button -l "reSort" -al "center" -w 60 -c "MR_OutlinerSort" MR_reSortA;
     setParent "..";
    setParent "..";
   setParent "..";

   separator -style "in" -w 260 -h 8;
   
   frameLayout -la "center" -cll 1 -cl 1 -bv 0 -l "Rename and Number"
    -cc "int $wPh=`window -q -h MultiRenamerWindow`;window -e -h ($wPh-126) MultiRenamerWindow;"
    -ec "int $wPh=`window -q -h MultiRenamerWindow`;window -e -h ($wPh+126) MultiRenamerWindow;"
	MR_RenameAndNumber;
    columnLayout;
     checkBox -l "KeepName" -al "left" -v 1 -w 256
      -onc "textFieldGrp -e -en 0 MR_numberRename;" 
      -ofc "textFieldGrp -e -en 1 MR_numberRename;" 
      MR_numKeepCheck;
     textFieldGrp -en 0 -l "Name" -tx "" -cw2 56 200 MR_numberRename;
     rowLayout -numberOfColumns 3 -columnWidth 1 62 -columnWidth 2 62 -columnWidth 3 130;
      checkBox -l "Front" -v 0 
       -onc "checkBox -e -v 0 MR_numBackCheck;" 
       -ofc "checkBox -e -v 1 MR_numBackCheck;" 
       MR_numFrontCheck;
      checkBox -l "Back" -v 1 
       -onc "checkBox -e -v 0 MR_numFrontCheck;" 
       -ofc "checkBox -e -v 1 MR_numFrontCheck;" 
       MR_numBackCheck;
      textFieldGrp -l "Sandwich" -tx "" -cw2 72 58 MR_numInsertionField;
     setParent "..";
     rowLayout -numberOfColumns 2 -columnWidth 1 124 -columnWidth 2 130;
      checkBox -l "EditStart" -v 0 -w 120
       -onc "intFieldGrp -e -en 1 MR_numberStart;" 
       -ofc "intFieldGrp -e -en 0 MR_numberStart;" 
       MR_numStartCheck;
      intFieldGrp -l "StartNumber" -v1 0 -en 0 -cw2 72 58 MR_numberStart;
     setParent "..";
     rowLayout -numberOfColumns 2 -columnWidth 1 124 -columnWidth 2 130;
      checkBox -l "autoPadding" -v 1 -w 120
       -onc "intFieldGrp -e -en 0 MR_numberPadding;" 
       -ofc "intFieldGrp -e -en 1 MR_numberPadding;" 
       MR_numAutoCheck;
      intFieldGrp -l "Padding" -v1 0 -en 0 -cw2 72 58 MR_numberPadding;
     setParent "..";
     rowColumnLayout -numberOfColumns 2 -columnWidth 1 190 -columnWidth 2 66;
      button -l "Rename And Alphabet" -al "center" -c "MR_NumberRename" MR_NumberRenameButton;
      button -l "reSort" -al "center" -w 60 -c "MR_OutlinerSort" MR_reSortB;
     setParent "..";
    setParent "..";
   setParent "..";

   separator -style "in" -w 260 -h 8;
   
   frameLayout -la "center" -cll 1 -cl 1 -bv 0 -l "Prefix and Suffix"
    -cc "int $wPh=`window -q -h MultiRenamerWindow`;window -e -h ($wPh-64) MultiRenamerWindow;"
    -ec "int $wPh=`window -q -h MultiRenamerWindow`;window -e -h ($wPh+64) MultiRenamerWindow;"
	MR_PrefixAndSuffix;
    columnLayout;
     rowColumnLayout -numberOfColumns 2 -columnWidth 1 54 -columnWidth 2 200;
      checkBox -l "Prefix" -v 1 -w 120
       -onc "textFieldGrp -e -en 1 MR_PrefixText;" 
       -ofc "textFieldGrp -e -en 0 MR_PrefixText;" 
       MR_PrefixCheck;
      textFieldGrp -tx "" MR_PrefixText;
      checkBox -l "Suffix" -v 1 -w 120
       -onc "textFieldGrp -e -en 1 MR_SuffixText;" 
       -ofc "textFieldGrp -e -en 0 MR_SuffixText;" 
       MR_SuffixCheck;
      textFieldGrp -tx "" MR_SuffixText;
     setParent "..";
     rowColumnLayout -numberOfColumns 2 -columnWidth 1 190 -columnWidth 2 66;
      button -l "Prefix and Suffix" -al "center"  -c "MR_PrefixSuffix" MR_PrefixAndSuffixButton;
      button -l "reSort" -al "center" -w 60 -c "MR_OutlinerSort" MR_reSortC;
     setParent "..";
    setParent "..";
   setParent "..";

   separator -style "in" -w 260 -h 8;
   
   frameLayout -la "center" -cll 1 -cl 1 -bv 0 -l "Search and Replace"
    -cc "int $wPh=`window -q -h MultiRenamerWindow`;window -e -h ($wPh-64) MultiRenamerWindow;"
    -ec "int $wPh=`window -q -h MultiRenamerWindow`;window -e -h ($wPh+64) MultiRenamerWindow;"
	MR_SearchAndReplace;
    columnLayout ;
     columnLayout ;
      textField -w 256 -tx "" MR_SearchText;
      textField -w 256 -tx "" MR_ReplaceText;
     setParent "..";
     rowColumnLayout -numberOfColumns 2 -columnWidth 1 190 -columnWidth 2 66;
      button -l "Search and Replace" -al "center"  -c "MR_SearchReplace" MR_SearchAndReplaceButton;
      button -l "reSort" -al "center" -w 60 -c "MR_OutlinerSort" MR_reSortD;
     setParent "..";
    setParent "..";
   setParent "..";

   separator -style "in" -w 260 -h 8;
   
   frameLayout -la "center" -cll 1 -cl 1 -bv 0 -l "Erase from Name"
    -cc "int $wPh=`window -q -h MultiRenamerWindow`;window -e -h ($wPh-44) MultiRenamerWindow;"
    -ec "int $wPh=`window -q -h MultiRenamerWindow`;window -e -h ($wPh+44) MultiRenamerWindow;"
	MR_EraseFromName;
    columnLayout;
     rowLayout -numberOfColumns 3 -columnWidth 1 62 -columnWidth 2 62 -columnWidth 3 130;
      checkBox -l "front" -v 1 
       -onc "checkBox -e -v 0 MR_delBackCheck;" 
       -ofc "checkBox -e -v 1 MR_delBackCheck;" 
       MR_delFrontCheck;
      checkBox -l "back" -v 0 
       -onc "checkBox -e -v 0 MR_delFrontCheck;" 
       -ofc "checkBox -e -v 1 MR_delFrontCheck;" 
       MR_delBackCheck;
      intFieldGrp -l "Shift" -v1 0 -cw2 72 58 MR_delShiftStart;
     setParent "..";
     rowColumnLayout -numberOfColumns 2 -columnWidth 1 100 -columnWidth 2 156;
      intFieldGrp -l "delNumber" -v1 3 -cw2 70 28 MR_delNumber;
      button -l "Erase From Name" -al "center" -c "MR_Erase" MR_EraseButton;
     setParent "..";
    setParent "..";
   setParent "..";

   separator -style "in" -w 260 -h 8;
   
  setParent "..";
 showWindow MultiRenamerWindow;
}
//MultiRenamer_ReadmeCommnd------------------------------------------------------------------------------------------------
global proc MultiRenamer_ReadmeCommnd(){
	string $htmlPath="";
	string $tempPath=`internalVar -userScriptDir`;
	string $tempPathList[];
	int $nm=`tokenize $tempPath "/" $tempPathList`;
	$htmlPath=$tempPathList[0];
	for($i=1;$i<$nm-1;$i++){$htmlPath=$htmlPath+"\\"+$tempPathList[$i];}
	$htmlPath = $htmlPath + "\\html\\MultiRenamer_readme.html";
	system("start explorer "+$htmlPath);
};
//URL------------------------------------------------------------------------
global proc UnitBus(){
	string $link = "start explorer \"http://mosquito.homeftp.net/\"";
	system($link);
};
global proc MultiRenamer_loadEnglish(){
  menu -e -label "Edit" MR_Edit;
  menuItem -e -label "ResetSettings" MR_Reset;
  menu -e -label "Language" MR_Language;
  menuItem -e -label "English" MR_English;
  menuItem -e -label "Japanese" MR_Japanese;
  menu -e -label "Help" MR_Help;
  menuItem -e -label "Readme" MR_Readme;
  menuItem -e -label "HomePeage" MR_HomePeage;
   
   frameLayout -e -l "Rename and Alphabet" MR_RenameAndAlphabet;
     checkBox -e -l "KeepName" MR_keepCheck;
     textFieldGrp -e -l "Name" MR_alphaRename;
      checkBox -e -l "Front" MR_frontCheck;
      checkBox -e -l "Back" MR_backCheck;
      textFieldGrp -e -l "Sandwich" MR_InsertionField;
      checkBox -e -l "EditStart" MR_startCheck;
      intFieldGrp -e -l "StartNumber" MR_alphaStart;
      checkBox -e -l "AutoPadding" MR_autoCheck;
      intFieldGrp -e -l "Padding" MR_alphaPadding;
      button -e -l "Rename And Alphabet" MR_AlphabetRenameButton;
      button -e -l "reSort" MR_reSortA;
   
   frameLayout -e -l "Rename and Number" MR_RenameAndNumber;
     checkBox -e -l "KeepName" MR_numKeepCheck;
     textFieldGrp -e -l "Name" MR_numberRename;
      checkBox -e -l "Front" MR_numFrontCheck;
      checkBox -e -l "Back" MR_numBackCheck;
      textFieldGrp -e -l "Sandwich" MR_numInsertionField;
      checkBox -e -l "EditStart" MR_numStartCheck;
      intFieldGrp -e -l "StartNumber" MR_numberStart;
      checkBox -e -l "autoPadding" MR_numAutoCheck;
      intFieldGrp -e -l "Padding" MR_numberPadding;
      button -e -l "Rename And Alphabet" MR_NumberRenameButton;
      button -e -l "reSort" MR_reSortB;
   
   frameLayout -e -l"Prefix and Suffix" MR_PrefixAndSuffix;
      checkBox -e -l "Prefix" MR_PrefixCheck;
      checkBox -e -l "Suffix" MR_SuffixCheck;
      button -e -l "Prefix and Suffix" MR_PrefixAndSuffixButton;
      button -e -l "reSort" MR_reSortC;
   
   frameLayout -e -l "Search and Replace" MR_SearchAndReplace;
      button -e -l "Search and Replace" MR_SearchAndReplaceButton;
      button -e -l "reSort" -al "center" MR_reSortD;
   
   frameLayout -e -l "Erase from Name" MR_EraseFromName;
      checkBox -e -l "front" MR_delFrontCheck;
      checkBox -e -l "back" MR_delBackCheck;
      intFieldGrp -e -l "Shift" MR_delShiftStart;
      intFieldGrp -e -l "delNumber"MR_delNumber;
      button -e -l "Erase From Name"MR_EraseButton;
};
global proc MultiRenamer_loadJapanese(){
  menu -e -label "設定" MR_Edit;
  menuItem -e -label "リセット" MR_Reset;
  menu -e -label "言語" MR_Language;
  menuItem -e -label "英語" MR_English;
  menuItem -e -label "日本語" MR_Japanese;
  menu -e -label "ヘルプ" MR_Help;
  menuItem -e -label "Readme" MR_Readme;
  menuItem -e -label "HomePeage" MR_HomePeage;
   
   frameLayout -e -l "アルファベット連番" MR_RenameAndAlphabet;
     checkBox -e -l "元の名前を使用" MR_keepCheck;
     textFieldGrp -e -l "名前" MR_alphaRename;
      checkBox -e -l "前" MR_frontCheck;
      checkBox -e -l "後" MR_backCheck;
      textFieldGrp -e -l "挟む文字" MR_InsertionField;
      checkBox -e -l "開始番号を変更" MR_startCheck;
      intFieldGrp -e -l "開始番号" MR_alphaStart;
      checkBox -e -l "自動連番桁" MR_autoCheck;
      intFieldGrp -e -l "連番桁" MR_alphaPadding;
      button -e -l "アルファベット連番を実行" MR_AlphabetRenameButton;
      button -e -l "並び替え" MR_reSortA;
   
   frameLayout -e -l "数字連番" MR_RenameAndNumber;
     checkBox -e -l "元の名前を使用" MR_numKeepCheck;
     textFieldGrp -e -l "名前" MR_numberRename;
      checkBox -e -l "前" MR_numFrontCheck;
      checkBox -e -l "後" MR_numBackCheck;
      textFieldGrp -e -l "挟む文字" MR_numInsertionField;
      checkBox -e -l "開始番号を変更" MR_numStartCheck;
      intFieldGrp -e -l "開始番号" MR_numberStart;
      checkBox -e -l "自動連番桁" MR_numAutoCheck;
      intFieldGrp -e -l "連番桁" MR_numberPadding;
      button -e -l "数字連番を実行" MR_NumberRenameButton;
      button -e -l "並び替え" MR_reSortB;
   
   frameLayout -e -l"接頭語と接尾語" MR_PrefixAndSuffix;
      checkBox -e -l "接頭語" MR_PrefixCheck;
      checkBox -e -l "接尾語" MR_SuffixCheck;
      button -e -l "接頭語と接尾語を追加" MR_PrefixAndSuffixButton;
      button -e -l "並び替え" MR_reSortC;
   
   frameLayout -e -l "検索と置き換え" MR_SearchAndReplace;
      button -e -l "検索と置き換えを実行" MR_SearchAndReplaceButton;
      button -e -l "並び替え" MR_reSortD;
   
   frameLayout -e -l "名前から削除" MR_EraseFromName;
      checkBox -e -l "前" MR_delFrontCheck;
      checkBox -e -l "後" MR_delBackCheck;
      intFieldGrp -e -l "ずらす" MR_delShiftStart;
      intFieldGrp -e -l "削除する数"MR_delNumber;
      button -e -l "名前から削除を実行"MR_EraseButton;
};


//MR_AlphabetRename------------------------------------------------------
global proc MR_AlphabetRename(){
 int $keepCheck = `checkBox -q -value MR_keepCheck`;
 string $rename=`textFieldGrp -q -tx  MR_alphaRename`;
 int $frontCheck = `checkBox -q -value MR_frontCheck`;
 int $backCheck = `checkBox -q -value MR_backCheck`;
 string $InsertionField=`textFieldGrp -q -tx  MR_InsertionField`;
 int $autoCheck = `checkBox -q -value MR_autoCheck`;
 int $padding = `intFieldGrp -q -v1 MR_alphaPadding`;
 int $startCheck = `checkBox -q -v MR_startCheck`;
 int $start = `intFieldGrp -q -v1 MR_alphaStart`;

 //アルファベットを数字で扱えるように変換
 string $alphbet[] = { "A","B","C","D","E","F","G","H","I","J","K","L","M","N","O","P","Q","R","S","T","U","V","W","X","Y","Z" } ;

 string $sel[]=`ls -sl`;
 int $objNum = size ( $sel );

 int $i = 0 ; 
 int $s = 0;
 int $nm = $objNum;
 
 if ( $startCheck == 1 ){
  $nm = $objNum + $start;
 }
 
 if ( $autoCheck == 1 ){
  do { $s = ` pow 26 $i `; $i++; } while ( (` pow 26 $i `) - (` pow 26 ( $i -1)`) < $nm );
 } else { $s = ` pow 26 $padding `; }
 if ( $s == 1 ){ $s = 0; }

 if ( $startCheck == 1 ){
  $s = $s + $start;
 }

 int $lp = 0; 
 int $count;
 string $AL = "" ;
 while ( $lp < $objNum ) {
  $count = $lp + $s ;

  int $amari = $count % 26;
  $AL = $alphbet[$amari] + $AL;
  $count = $count / 26;

   while ( $count > 25 ) {
    $amari = $count % 26;
    $AL = $alphbet[$amari] + $AL;
    $count=$count/26;
   };
  while ( $count > 0 ) {
   $amari = $count % 26;
   $AL = $alphbet[$amari-1] + $AL ;
   $count = $count / 26 -1 ;
  };
  
  string $GetName[];
  int $GetNameN = ` tokenize $sel[$lp] "|" $GetName ` ;

	string $NewName;
	if ( $keepCheck == 0 && $frontCheck == 1){
		$NewName = ( $InsertionField + $AL + $rename);
		if ( catchQuiet ( ` rename $sel[$lp] $NewName ` ) ) { //エラーが起きたら下を実行
			rename $sel[$lp] ("_" + $NewName);
		}
	}
	if ( $keepCheck == 0 && $backCheck == 1) {
		$NewName = ( $rename + $AL + $InsertionField);
		if ( catchQuiet ( ` rename $sel[$lp] $NewName ` ) ) { //エラーが起きたら下を実行
			rename $sel[$lp] ("_" + $NewName);
		}
	}
	if ( $keepCheck == 1 && $frontCheck == 1){
		$NewName = ( $InsertionField + $AL + $GetName[$GetNameN-1]);
		if ( catchQuiet ( ` rename $sel[$lp] $NewName ` ) ) { //エラーが起きたら下を実行
			rename $sel[$lp] ("_" + $NewName);
		}
	}
	if ( $keepCheck == 1 && $backCheck == 1) {
		$NewName = ( $GetName[$GetNameN-1] + $AL + $InsertionField);
		if ( catchQuiet ( ` rename $sel[$lp] $NewName ` ) ) { //エラーが起きたら下を実行
			rename $sel[$lp] ("_" + $NewName);
		}
	}
	$AL="";
	$lp++;
	};
};

//MR_NumberRename------------------------------------------------------
global proc MR_NumberRename(){
 int $keepCheck = `checkBox -q -value MR_numKeepCheck`;
 string $rename=`textFieldGrp -q -tx  MR_numberRename`;
 int $frontCheck = `checkBox -q -value MR_numFrontCheck`;
 int $backCheck = `checkBox -q -value MR_numBackCheck`;
 string $InsertionField=`textFieldGrp -q -tx  MR_numInsertionField`;
 int $autoCheck = `checkBox -q -value MR_numAutoCheck`;
 int $padding = `intFieldGrp -q -v1 MR_numberPadding`;
 int $startCheck = `checkBox -q -v MR_numStartCheck`;
 int $start = `intFieldGrp -q -v1 MR_numberStart`;
 
 string $sel[]=`ls -sl`;
 int $objNum = size ( $sel );
  
 int $s = 0;
 if ( $startCheck == 1) { $s = $start ; }
 
 int $lp = 0; 
 int $count;
 while ( $lp < $objNum ) {
 
  string $pad = $s + $lp;
  int $cnt = size ( $pad );
  string $nn = $objNum + $s;
  int $nm = size ( $nn );
  if ( $autoCheck == 0) {  $nm =  $padding; }
  
  for ( $i = 0; $i < ( $nm - $cnt ); $i++ ){
   $pad = "0" + $pad;
  }
   
  string $GetName[];
  int $GetNameN = ` tokenize $sel[$lp] "|" $GetName ` ;
  
  if ( $frontCheck == 1){ if ( $InsertionField == "" ) { $InsertionField = "_";} }

	string $NewName;
	if ( $keepCheck == 0 && $frontCheck == 1){
		$NewName = ( $InsertionField + $pad + $rename);
		if ( catchQuiet ( ` rename $sel[$lp] $NewName ` ) ) { //エラーが起きたら下を実行
			rename $sel[$lp] ("_" + $NewName);
		}
	}
	if ( $keepCheck == 0 && $backCheck == 1) {
		$NewName = ( $rename + $pad + $InsertionField);
		if ( catchQuiet ( ` rename $sel[$lp] $NewName ` ) ) { //エラーが起きたら下を実行
			rename $sel[$lp] ("_" + $NewName);
		}
	}
	if ( $keepCheck == 1 && $frontCheck == 1){
		$NewName = ( $InsertionField + $pad + $GetName[$GetNameN-1]);
		if ( catchQuiet ( ` rename $sel[$lp] $NewName ` ) ) { //エラーが起きたら下を実行
			rename $sel[$lp] ("_" + $NewName);
		}
	}
	if ( $keepCheck == 1 && $backCheck == 1) {
		$NewName = ( $GetName[$GetNameN-1] + $pad + $InsertionField);
		if ( catchQuiet ( ` rename $sel[$lp] $NewName ` ) ) { //エラーが起きたら下を実行
			rename $sel[$lp] ("_" + $NewName);
		}
	}
	$lp++;
	};
};

//MR_OutlinerSort------------------------------------------------------
global proc MR_OutlinerSort(){
 string $sel[]=`ls -sl`;
 $sel=sort($sel);

 int $lp=0;
 int $nm=`size $sel`;
 while($lp<$nm){
  reorder -b $sel[$lp];
  $lp++;
 };
};

//MR_SearchReplace------------------------------------------------------
global proc int MR_SearchReplace(){
	string $SearchText = ` textField -q -tx MR_SearchText `;
	string $ReplaceText = ` textField -q -tx MR_ReplaceText `;
	
	string $sel[] = ` ls -sl -fl `;
	int $lp = 0;
	int $nm = ` size $sel `;	
	if ( 0 < $nm ) {
		print ( "選択されたオブジェクトをリネームします。\n");
	} else {
		warning ( "オブジェクトが選択されてません。\n");
		return 0;
	}
	while ( $lp < $nm ){
		string $DivName[];
		int $DivNameN=`tokenize $sel[$lp] "|" $DivName`;
		string $GetName = $DivName[$DivNameN-1];
		
		string $NewName = substituteAllString( $GetName, $SearchText, $ReplaceText);
		if ( catchQuiet ( ` rename $sel[$lp] $NewName ` ) ) { //エラーが起きたら下を実行
			rename $sel[$lp] ("_" + $NewName);
		}

		//print ( $sel[$lp] + "を" + $NewName + "に変換しました。\n");
		$lp++;
	};
	return 1;
};

//MR_PrefixSuffix------------------------------------------------------
global proc int MR_PrefixSuffix(){
	int $PrefixCheck = ` checkBox -q -v MR_PrefixCheck `;
	int $SuffixCheck = ` checkBox -q -v MR_SuffixCheck `;
	string $PrefixText = ` textFieldGrp -q -tx MR_PrefixText `;
	string $SuffixText = ` textFieldGrp -q -tx MR_SuffixText `;
	
	if ( $PrefixCheck == 0 && $SuffixCheck == 0) {
		warning ( "PrefixかSuffixのどちらかにチェックいれてください。\n");
		return 0;
	}
	
	string $sel[]=`ls -sl -fl`;
	int $lp=0;
	int $nm=`size $sel`;
	while ( $lp < $nm ){
		string $DivName[];
		int $DivNameN=`tokenize $sel[$lp] "|" $DivName`;
		string $GetName = $DivName[$DivNameN-1];
		
		string $NewName = $GetName;
		if ( $PrefixCheck == 1 && $SuffixCheck == 1) {
			$NewName = ( $PrefixText + $GetName + $SuffixText );
			if ( catchQuiet ( ` rename $sel[$lp] $NewName ` ) ) { //エラーが起きたら下を実行
				rename $sel[$lp] ("_" + $NewName);
			}
		} else if ( $PrefixCheck == 1 ) {
			$NewName = ( $PrefixText + $GetName );
			if ( catchQuiet ( ` rename $sel[$lp] $NewName ` ) ) { //エラーが起きたら下を実行
				rename $sel[$lp] ("_" + $NewName);
			}
		} else if ( $SuffixCheck == 1) {
			$NewName = ( $GetName + $SuffixText );
			if ( catchQuiet ( ` rename $sel[$lp] $NewName ` ) ) { //エラーが起きたら下を実行
				rename $sel[$lp] ("_" + $NewName);
			}
		}
		$lp++;
	};
	return 1;
};

//MR_Erase------------------------------------------------------
global proc MR_Erase(){
	int $delFrontCheck = `checkBox -q -value MR_delFrontCheck`;
	int $delBackCheck = `checkBox -q -value MR_delBackCheck`;
	int $shiftStart = `intFieldGrp -q -v1 MR_delShiftStart`;
	int $delNumber = `intFieldGrp -q -v1 MR_delNumber`;
	
	string $sel[]=`ls -sl -fl`;
	int $lp=0;
	int $nm=`size $sel`;
	while ( $lp < $nm ){
	string $NewName="";
	string $GetName[];
	int $GetNameN=`tokenize $sel[$lp] "|" $GetName`;
	int $naN=`size($GetName[$GetNameN-1])`;
	
	if ( $naN > ( $delNumber + $shiftStart ) ){
		if ( $delFrontCheck == 1 ){
			if ( $shiftStart > 0 ){
				$NewName = ` substring $GetName[$GetNameN-1] 1 $shiftStart `;
			}
			$NewName = $NewName + (` substring $GetName[ $GetNameN -1 ] ( $shiftStart + $delNumber + 1) $naN `);
			if ( catchQuiet ( `rename $sel[$lp] $NewName` ) ) { //エラーが起きたら下を実行
				rename $sel[$lp] ("_" + $NewName);
			}
		}
		
		if ( $delBackCheck == 1){
			if ( $shiftStart > 0) {
				$NewName = ` substring $GetName[ $GetNameN -1 ] ( $naN-$shiftStart +1 ) $naN `;
			}
			$NewName =(` substring $GetName[ $GetNameN-1 ] 1 ( $naN-$delNumber - $shiftStart)`) + $NewName;
			if ( catchQuiet ( `rename $sel[$lp] $NewName` ) ) { //エラーが起きたら下を実行
				rename $sel[$lp] ("_" + $NewName);
			}
		}
	} else { warning ($sel[$lp]+"は変更を実行すると名前がなくなるので無視します"); }
	$lp++;
	};
};

//実行コマンド(shelf登録)
