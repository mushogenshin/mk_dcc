///////////////////////////////////////////////////////////////////////////////////////////////////
/*
	sdv_flattenUVs.mel
	==================
	This script flattens UVs.
	
	Usage:
	sdv_flattenUVs(0) -> Flatten to U center
	sdv_flattenUVs(1) -> Flatten to V center
	sdv_flattenUVs(2) -> Flatten to both U and V center
	sdv_flattenUVs(3) -> Flatten to left  
	sdv_flattenUVs(4) -> Flatten to right 
	sdv_flattenUVs(5) -> Flatten to top   
	sdv_flattenUVs(6) -> Flatten to bottom
	
	01 Aug 2007
	© Stefan David
	www.stefandavid.com
*/
///////////////////////////////////////////////////////////////////////////////////////////////////


global proc sdv_flattenUVs(int $mode) {
	global string $gSelect;
	string $sel[] = `ls -sl`;
	string $uvs[] = `filterExpand -sm 35`;
	if (!`size($uvs)`) error("No UVs selected");
	
	setToolTo $gSelect;	
	select -r $uvs;
	
	float $shellBox[] = `polyEvaluate -bc2`;
	float $centerU = 0.5 * ($shellBox[0]+$shellBox[1]);
	float $centerV = 0.5 * ($shellBox[2]+$shellBox[3]);
					
	if ($mode==0)	   polyEditUV -r 0 -u $centerU;					//center U
	else if ($mode==1) polyEditUV -r 0 -v $centerV;					//center V
	else if ($mode==2) polyEditUV -r 0 -u $centerU -v $centerV;		//center UV
	else if ($mode==3) polyEditUV -r 0 -u $shellBox[0];				//left
	else if ($mode==4) polyEditUV -r 0 -u $shellBox[1];				//right
	else if ($mode==5) polyEditUV -r 0 -v $shellBox[3];				//top
	else if ($mode==6) polyEditUV -r 0 -v $shellBox[2];				//bottom
	else {
		string $message = "Wrong call to sdv_flattenUVs. See script editor for valid options.\n";
		$message += "sdv_flattenUVs(0) -> Flatten to U center\n";
		$message += "sdv_flattenUVs(1) -> Flatten to V center\n";
		$message += "sdv_flattenUVs(2) -> Flatten to both U and V center\n";
		$message += "sdv_flattenUVs(3) -> Flatten to left\n";  
		$message += "sdv_flattenUVs(4) -> Flatten to right\n"; 
		$message += "sdv_flattenUVs(5) -> Flatten to top\n";   
		$message += "sdv_flattenUVs(6) -> Flatten to bottom\n";
		error($message);
	}
	
	select -r $sel;
}
