/////////////////////////////////////////////////////////////////////////////////////
//
// tCreaseTool.mel v1.10
//
// Tran Anh Thu
// February 19, 2014
//
// July 03, 2014
// Add Select section
//
// July 22, 2015
// Add Convert Hard Edge to Crease section
//
// June 01, 2016
// Modified by huuduy@virtuos-sparx.com for integration into ILM Tools
//
/////////////////////////////////////////////////////////////////////////////////////

global proc creaseTool()
{ 
	if(`window -q -exists tCreaseToolUI`) deleteUI tCreaseToolUI;
	window -t "Thu Crease Tool" -wh 293 320 -s 0 tCreaseToolUI;

frameLayout -l "Crease Value" valueFrame;
	columnLayout;
    	radioButtonGrp -numberOfRadioButtons 2 -label "Max Crease Value:"  -labelArray2 "Limited to 2" "Unlimited" -cat 1 "left" 5 -cw3 100 100 80 -ct3 "both" "both" "both" -sl 2 -cc("tCreaseTool_optionUpdate") tCreaseToolOption;
	setParent..;
	separator -style "in";

	rowLayout -numberOfColumns 3 -cat 1 "left" 3 -cw3 200 40 40 -ct3 "both" "both" "both";
	floatSliderGrp -bgc 0.267 0.267 0.267 -cw 1 60 -field true -min 0.00 -max 2.00 -fmn 0.00 -fmx 2.00  -pre 2 -dc ("tCreaseTool_command 1 -2") tCreaseToolValue;
	button -l "  Get  " -ann "Get Crease Value from selected components" -c ("tCreaseTool_getValue");
	button -l "  Set  " -ann "Set Crease Value to selected components" -c ("tCreaseTool_command 1 -2");
	setParent..; 

frameLayout -collapsable true  -cl 0 -l "Select Components" selectFrame;
	columnLayout;
    	radioButtonGrp -numberOfRadioButtons 2 -label "Working on:"  -labelArray2 "Vertices" "Edges" -cat 1 "left" 5 -cw3 100 100 80 -ct3 "both" "both" "both" -sl 2  tCreaseToolWorkOption;

	rowLayout -numberOfColumns 3 -cat 1 "left" 3 -cw3 120 80 80 -ct3 "both" "both" "both";
	floatFieldGrp -numberOfFields 1 -pre 2 -cal 1 "left" -cw2 60 40 -ct2 "both" "both" -label "Value:  From  " -value1 4.01 tCreaseToolValueSelectMin;
	floatFieldGrp -numberOfFields 1 -pre 2 -cal 1 "left" -cw2 40 40 -ct2 "both" "both" -label "     To" -value1 10.0 tCreaseToolValueSelectMax;
	button -l "  Select  " -ann "Select components with this crease value" -c ("$valMin = `floatFieldGrp -q -value1 tCreaseToolValueSelectMin`; $valMax = `floatFieldGrp -q -value1 tCreaseToolValueSelectMax`; $mode = `radioButtonGrp -q -sl tCreaseToolWorkOption`; tCreaseTool_select($valMin, $valMax, $mode)");
	setParent..; setParent..;setParent..;

frameLayout -l "Convert Hard Edge to Crease" hardFrame;
	rowLayout -numberOfColumns 2 -cat 1 "left" 3 -cw2 80 200 -ct2 "both" "both";
	intField -min 1 -max 10 -v 4 -s 1 -ann "Hard Edge to Crease Value" tCreaseToolHardValue;
	button -l " Convert  " -c ("string $hardEdges[] =  tCreaseTool_selectHarEdge(); select -r $hardEdges; tCreaseTool_command 1 `intField -q -v tCreaseToolHardValue`;");
	setParent..; setParent..;

frameLayout -l "Set Absolute Crease Value" absFrame;
	rowColumnLayout -numberOfColumns 11 ;
	button -l "0.0" -bgc 0.55 0.55 0.55 -c ("tCreaseTool_command 1 `button -q -l tCreaseTool_button_0`;") tCreaseTool_button_0;
	button -l "0.2" -c ("tCreaseTool_command 1 `button -q -l tCreaseTool_button_1`;") tCreaseTool_button_1;
	button -l "0.4" -c ("tCreaseTool_command 1 `button -q -l tCreaseTool_button_2`;") tCreaseTool_button_2;
	button -l "0.6" -c ("tCreaseTool_command 1 `button -q -l tCreaseTool_button_3`;") tCreaseTool_button_3;
	button -l "0.8" -c ("tCreaseTool_command 1 `button -q -l tCreaseTool_button_4`;") tCreaseTool_button_4;
	button -l "1.0" -bgc 0.55 0.55 0.55 -c ("tCreaseTool_command 1 `button -q -l tCreaseTool_button_5`;") tCreaseTool_button_5;
	button -l "1.2" -c ("tCreaseTool_command 1 `button -q -l tCreaseTool_button_6`;") tCreaseTool_button_6;
	button -l "1.4" -c ("tCreaseTool_command 1 `button -q -l tCreaseTool_button_7`;") tCreaseTool_button_7;
	button -l "1.6" -c ("tCreaseTool_command 1 `button -q -l tCreaseTool_button_8`;") tCreaseTool_button_8;
	button -l "1.8" -c ("tCreaseTool_command 1 `button -q -l tCreaseTool_button_9`;") tCreaseTool_button_9;
	button -l "2.0" -bgc 0.55 0.55 0.55 -c ("tCreaseTool_command 1 `button -q -l tCreaseTool_button_10`;")tCreaseTool_button_10;
	setParent..; setParent..;
  
frameLayout -l "Set Relative Crease Value" relFrame;
	rowLayout -numberOfColumns 3 -cat 1 "left" 3 -cw3 80 100 100 -ct3 "both" "both" "both";
	floatField -min 0.0 -max 2.0 -v 0.10 -pre 2 -ann "Increasement of + / - Buttons" tCreaseToolIncrease;
    	popupMenu -p tCreaseToolIncrease tCrease_Increase;
            menuItem -l "0.1" -c("floatField -e -v 0.1 tCreaseToolIncrease");
            menuItem -l "0.2" -c("floatField -e -v 0.2 tCreaseToolIncrease");
            menuItem -l "0.3" -c("floatField -e -v 0.3 tCreaseToolIncrease");
            menuItem -l "0.4" -c("floatField -e -v 0.4 tCreaseToolIncrease");
            menuItem -l "0.5" -c("floatField -e -v 0.5 tCreaseToolIncrease");
            menuItem -l "1.0" -c("floatField -e -v 1.0 tCreaseToolIncrease");
            menuItem -l "2.0" -c("floatField -e -v 2.0 tCreaseToolIncrease");
            menuItem -l "3.0" -c("floatField -e -v 3.0 tCreaseToolIncrease");
            menuItem -l "4.0" -c("floatField -e -v 4.0 tCreaseToolIncrease");
            menuItem -l "5.0" -c("floatField -e -v 5.0 tCreaseToolIncrease");

	button -l " -  " -c ("tCreaseTool_command -1 -1");
	button -l " +  " -c ("tCreaseTool_command 1 -1");
	setParent..; setParent..;

frameLayout -collapsable true  -cl 0 -l "Copy Crease Value" copyFrame;
    rowLayout -numberOfColumns 2 -cat 1 "left" 3 -cw2 200 80 -ct2 "both" "both";
    text -l "Select source object first";
    button -l "  Copy  " -ann "Copy crease value from 1st selected object to other (duplicated ) selected object(s)" -c ("tCreaseToolCopy");
    setParent..; setParent..;	

window -e -wh 293 450 tCreaseToolUI;
showWindow tCreaseToolUI;
tCreaseTool_optionUpdate;
}


/////////////////////////////////////////////////////////////////////////////////////
//
// tCreaseTool_command:
//
// direction: -1: minus, 1: add
// value: -2: use floatSliderGrp tCreaseToolValue, -1: use +/- button, other: use Absolute value
//
/////////////////////////////////////////////////////////////////////////////////////

global proc tCreaseTool_command(int $direction, float $value)
{
	string $allSelection[] = `ls -sl`;
	for($aSelection in $allSelection)
	{
    	select -r $aSelection;
    	if($value == -1) // user press + or - button
    	{
            float $increase = `floatField -q -v tCreaseToolIncrease`;
            $increase *= $direction;
            
            if($direction == 1 && `floatSliderGrp -q -v tCreaseToolValue` >= 2.0 && `radioButtonGrp -q -sl tCreaseToolOption` == 1)
                return;
            if($direction == 1 && `floatSliderGrp -q -v tCreaseToolValue` >= 10.0 && `radioButtonGrp -q -sl tCreaseToolOption` == 2)
                return;
                
            polyCrease -ch 1 -relativeValue $increase;
            tCreaseTool_getValue;
        }
    	else if($value == -2) // user drag floatSliderGrp tCreaseToolValue slider or press Set button
    	{
            $value = `floatSliderGrp -q -v tCreaseToolValue`;
            polyCrease -ch true -value $value -vertexValue $value;
        }    
        else // user press [0 .... 2] button
        {
            polyCrease -ch true -value $value -vertexValue $value;
            tCreaseTool_getValue;
        }
    }
    select -r $allSelection;
}
/////////////////////////////////////////////////////////////////////////////////////

global proc tCreaseTool_getValue()
{
    string $tCreaseTool_selectedComponents[];
	float $allValue[];
	float $sumValue = 0.0;
	float $averageValue;
	
	$tCreaseTool_selectedComponents = `filterExpand -sm 32`; // edges selected
	if(size($tCreaseTool_selectedComponents) > 0)
	{
        $allValue = `polyCrease -q -value $tCreaseTool_selectedComponents`;
    }
    else
    {
        $tCreaseTool_selectedComponents = `filterExpand -sm 31`; // vertex selected
        $allValue = `polyCrease -q -vertexValue $tCreaseTool_selectedComponents`;
    }
    
    for($aValue in $allValue)
    {
        $sumValue += $aValue;
    }
    $averageValue = $sumValue / size($allValue);
    
    if($averageValue > `floatSliderGrp -q -max tCreaseToolValue`)
    {
        floatSliderGrp -e -bgc 0.4 0.4 0.4 tCreaseToolValue;
    }
    else
    {
        floatSliderGrp -e -bgc 0.267 0.267 0.267 tCreaseToolValue;
    }
    floatSliderGrp -e -v $averageValue tCreaseToolValue;
}
/////////////////////////////////////////////////////////////////////////////////////

global proc tCreaseTool_optionUpdate()
{
    int $limit_Option = `radioButtonGrp -q -sl tCreaseToolOption`;
    
    if($limit_Option == 1)
    {
        floatSliderGrp -e -min 0.00 -max 2.00 -fmn 0.00 -fmx 2.00  -v 2.0 tCreaseToolValue;

        button -e -l "0.0" tCreaseTool_button_0;
        button -e -l "0.2" tCreaseTool_button_1;
        button -e -l "0.4" tCreaseTool_button_2;
        button -e -l "0.6" tCreaseTool_button_3;
        button -e -l "0.8" tCreaseTool_button_4;
        button -e -l "1.0" tCreaseTool_button_5;
        button -e -l "1.2" tCreaseTool_button_6;
        button -e -l "1.4" tCreaseTool_button_7;
        button -e -l "1.6" tCreaseTool_button_8;
        button -e -l "1.8" tCreaseTool_button_9;
        button -e -l "2.0" tCreaseTool_button_10;
        
        floatField -e -min 0.0 -max 2.0 -v 0.10 tCreaseToolIncrease;
        
        popupMenu -e -deleteAllItems tCrease_Increase;
            menuItem -l "0.1" -c("floatField -e -v 0.1 tCreaseToolIncrease");
            menuItem -l "0.2" -c("floatField -e -v 0.2 tCreaseToolIncrease");
            menuItem -l "0.3" -c("floatField -e -v 0.3 tCreaseToolIncrease");
            menuItem -l "0.4" -c("floatField -e -v 0.4 tCreaseToolIncrease");
            menuItem -l "0.5" -c("floatField -e -v 0.5 tCreaseToolIncrease");
            menuItem -l "1.0" -c("floatField -e -v 1.0 tCreaseToolIncrease");

    }
        
    else if($limit_Option == 2)
    {
        floatSliderGrp -e -min 0.00 -max 10.00 -fmn 0.00 -fmx 10.00 -v 10.0 tCreaseToolValue;

        button -e -l "0.0" tCreaseTool_button_0;
        button -e -l "1.0" tCreaseTool_button_1;
        button -e -l "2.0" tCreaseTool_button_2;
        button -e -l "3.0" tCreaseTool_button_3;
        button -e -l "4.0" tCreaseTool_button_4;
        button -e -l "5.0" tCreaseTool_button_5;
        button -e -l "6.0" tCreaseTool_button_6;
        button -e -l "7.0" tCreaseTool_button_7;
        button -e -l "8.0" tCreaseTool_button_8;
        button -e -l "9.0" tCreaseTool_button_9;
        button -e -l " 10 " tCreaseTool_button_10;
        
        floatField -e -min 0.0 -max 10.0 -v 1.0 tCreaseToolIncrease;
        
        popupMenu -e -deleteAllItems tCrease_Increase;
            menuItem -l "0.1" -c("floatField -e -v 0.1 tCreaseToolIncrease");
            menuItem -l "0.2" -c("floatField -e -v 0.2 tCreaseToolIncrease");
            menuItem -l "0.3" -c("floatField -e -v 0.3 tCreaseToolIncrease");
            menuItem -l "0.4" -c("floatField -e -v 0.4 tCreaseToolIncrease");
            menuItem -l "0.5" -c("floatField -e -v 0.5 tCreaseToolIncrease");
            menuItem -l "1.0" -c("floatField -e -v 1.0 tCreaseToolIncrease");
            menuItem -l "2.0" -c("floatField -e -v 2.0 tCreaseToolIncrease");
            menuItem -l "3.0" -c("floatField -e -v 3.0 tCreaseToolIncrease");
            menuItem -l "4.0" -c("floatField -e -v 4.0 tCreaseToolIncrease");
            menuItem -l "5.0" -c("floatField -e -v 5.0 tCreaseToolIncrease");

    }
}
/////////////////////////////////////////////////////////////////////////////////////;

global proc tCreaseTool_select(float $valMin, float $valMax, int $mode)
{
    string $tCreaseTool_selectedComponents[];
    string $tCreaseTool_returnComponents[] = {};
	if($mode == 2)
	{
    	ConvertSelectionToEdges;
    	$tCreaseTool_selectedComponents = `filterExpand -sm 32`; // edges selected
    	if(size($tCreaseTool_selectedComponents) > 0)
    	{
            for($aEdge in $tCreaseTool_selectedComponents)
            {
            	float $aValue[] = `polyCrease -q -value $aEdge`;
                if(($aValue[0] >= $valMin) && ($aValue[0] <= $valMax))
                {
                    $tCreaseTool_returnComponents[size($tCreaseTool_returnComponents)] = $aEdge;
                }
            }
        }
    }
    else if ($mode == 1)
    {
        ConvertSelectionToVertices;
        $tCreaseTool_selectedComponents = `filterExpand -sm 31`; // vertex selected
    	if(size($tCreaseTool_selectedComponents) > 0)
    	{
            for($aVtx in $tCreaseTool_selectedComponents)
            {
                float $aValue[] = `polyCrease -q -vertexValue $aVtx`;
                if(($aValue[0] >= $valMin) && ($aValue[0] <= $valMax))
                {
                    $tCreaseTool_returnComponents[size($tCreaseTool_returnComponents)] = $aVtx;
                }
            }
        }
    }

select -r $tCreaseTool_returnComponents;

}
/////////////////////////////////////////////////////////////////////////////////////
global proc string[] tCreaseTool_selectHarEdge()
{
    string $hardEdges[] = {};
    string $allEdge[] = `polyListComponentConversion -te`;
    $allEdge = `filterExpand -ex 1 -sm 32 $allEdge`;
    for($aEdge in $allEdge)
    {
        string $edgeType[] = `polyInfo -ev $aEdge`;
        if (`gmatch $edgeType[0] "*Hard*"`)
            $hardEdges[size($hardEdges)] = $aEdge;
    }
    return $hardEdges;
}
//////////////////////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////////////////////
// copy crease value from 1st selected object to other selected object(s)
global proc tCreaseToolCopy()
{
    string $aSel[] = `ls -sl`;
    string $source = $aSel[0];
    
    select -r $aSel[0];
    ConvertSelectionToEdges;
    string $sourceEdge[] = `filterExpand -sm 32`; // edges selected
    float $sourceValue[];
    int $edge = 0;
    for($edge = 0; $edge < size($sourceEdge); $edge ++)
    {
        float $edgeValue[] = `polyCrease -q -value $sourceEdge[$edge]`;
        $sourceValue[$edge] = $edgeValue[0];
    }
    
    int $i ;
    for($i = 1; $i < size($aSel); $i ++)
    {
        select -r $aSel[$i];
        ConvertSelectionToEdges;
        string $destEdge[] = `filterExpand -sm 32`;
        
        if(size($sourceEdge) != size($destEdge))
        {
            warning ("Object " + $aSel[$i] + " not identical with " +  $source + ", skiped");
            continue;
        }
        // clear all crease value
        polyCrease -ch true -value 0 -vertexValue 0;
        
        int $edge;
        for($edge = 0; $edge < size($sourceValue); $edge++)
        {
            polyCrease -ch true -value $sourceValue[$edge] -vertexValue $sourceValue[$edge] ($aSel[$i] + ".e[" + $edge + "]");
        }
    }
    select -r $aSel;
}
/////////////////////////////////////////////////////////////////////////////////////


/////////////////////////////////////////////////////////////////////////////////////
// for ILM Tools only
global proc tCreaseTool_command_ilm(float $value)
{
    string $allSelection[] = `ls -sl`;
    for($aSelection in $allSelection)
    {
        select -r $aSelection;   
        polyCrease -ch true -value $value -vertexValue $value;
    }
    select -r $allSelection;
}
